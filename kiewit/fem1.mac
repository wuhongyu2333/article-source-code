finish
/clear
/filname,fem1

!!输入数据
*DIM,INPUTDATA,array,11,1
*VREAD,INPUTDATA,D:\SOFTWARE\ANSYS\dome_2\input,txt,,IJK,
(1f6.0)


!!参数输入
/prep7
!!材料特性
PI=ACOS(-1)												!!pi
Ex=2.06e11												!!弹性模量
Nu=0.3														!!泊松比
Fz=100000          				!!节点荷载
!!几何特性
dead_load=3000						!恒载3kN/m2
live_load=1500						!活载1.5kN/m2
wind_load=1000						!风载1kN/m2


span=80
R=span/2
H=INPUTDATA(1)	    							!!矢高
m=5									!!环数
n=INPUTDATA(2)       							!!内环段数
m1=INPUTDATA(3)
m2=INPUTDATA(4)
m3=INPUTDATA(5)
m4=INPUTDATA(6)
m5=INPUTDATA(7)
m6=INPUTDATA(8)
m7=INPUTDATA(9)
m8=INPUTDATA(10)
m9=INPUTDATA(11)
d_ring=R/m   										!!环间距
arc=(R**2+H**2)/(2*H) 									!!立面圆弧半径


!!定义结构
/prep7

!!定义截面
et,1,beam188       										!!定义单元类型
mp,ex,1,Ex          										!!定义弹性模量
mp,nuxy,1,Nu   										!!定义泊松比
mp,dens,1,7850 										!!定义密度

sectype,1,beam,I,,1
secoffset,cent
secdata,68e-3,68e-3,100e-3,7.6e-3,7.6e-3,4.5e-3
sectype,2,beam,I,,1
secoffset,cent
secdata,74e-3,74e-3,126e-3,8.4e-3,8.4e-3,5e-3
sectype,3,beam,I,,1
secoffset,cent
secdata,80e-3,80e-3,140e-3,9.1e-3,9.1e-3,5.5e-3
sectype,4,beam,I,,1
secoffset,cent
secdata,88e-3,88e-3,160e-3,9.9e-3,9.9e-3,6e-3
sectype,5,beam,I,,1
secoffset,cent
secdata,94e-3,94e-3,180e-3,10.7e-3,10.7e-3,6.5e-3
sectype,6,beam,I,,1
secoffset,cent
secdata,100e-3,100e-3,200e-3,11.4e-3,11.4e-3,7e-3
sectype,7,beam,I,,1
secoffset,cent
secdata,102e-3,102e-3,200e-3,11.4e-3,11.4e-3,9e-3
sectype,8,beam,I,,1
secoffset,cent
secdata,110e-3,110e-3,220e-3,12.3e-3,12.3e-3,7.5e-3
sectype,9,beam,I,,1
secoffset,cent
secdata,112e-3,112e-3,220e-3,12.3e-3,12.3e-3,9.5e-3
sectype,10,beam,I,,1
secoffset,cent
secdata,116e-3,116e-3,250e-3,13e-3,13e-3,8e-3
sectype,11,beam,I,,1
secoffset,cent
secdata,118e-3,118e-3,250e-3,13e-3,13e-3,10e-3
sectype,12,beam,I,,1
secoffset,cent
secdata,122e-3,122e-3,280e-3,13.7e-3,13.7e-3,8.5e-3
sectype,13,beam,I,,1
secoffset,cent
secdata,124e-3,124e-3,280e-3,13.7e-3,13.7e-3,10.5e-3
sectype,14,beam,I,,1
secoffset,cent
secdata,130e-3,130e-3,320e-3,15e-3,15e-3,9.5e-3
sectype,15,beam,I,,1
secoffset,cent
secdata,132e-3,132e-3,320e-3,15e-3,15e-3,11.5e-3
sectype,16,beam,I,,1
secoffset,cent
secdata,134e-3,134e-3,320e-3,15e-3,15e-3,13.5e-3
sectype,17,beam,I,,1
secoffset,cent
secdata,136e-3,136e-3,360e-3,15.8e-3,15.8e-3,10e-3
sectype,18,beam,I,,1
secoffset,cent
secdata,138e-3,138e-3,360e-3,15.8e-3,15.8e-3,12e-3
sectype,19,beam,I,,1
secoffset,cent
secdata,140e-3,140e-3,360e-3,15.8e-3,15.8e-3,14e-3
sectype,20,beam,I,,1
secoffset,cent
secdata,142e-3,142e-3,400e-3,16.5e-3,16.5e-3,10.5e-3
sectype,21,beam,I,,1
secoffset,cent
secdata,144e-3,144e-3,400e-3,16.5e-3,16.5e-3,12.5e-3
sectype,22,beam,I,,1
secoffset,cent
secdata,146e-3,146e-3,400e-3,16.5e-3,16.5e-3,14.5e-3
sectype,23,beam,I,,1
secoffset,cent
secdata,150e-3,150e-3,450e-3,18e-3,18e-3,11.5e-3
sectype,24,beam,I,,1
secoffset,cent
secdata,152e-3,152e-3,450e-3,18e-3,18e-3,13.5e-3
sectype,25,beam,I,,1
secoffset,cent
secdata,154e-3,154e-3,450e-3,18e-3,18e-3,15.5e-3
sectype,26,beam,I,,1
secoffset,cent
secdata,158e-3,158e-3,500e-3,20e-3,20e-3,12e-3
sectype,27,beam,I,,1
secoffset,cent
secdata,160e-3,160e-3,500e-3,20e-3,20e-3,14e-3
sectype,28,beam,I,,1
secoffset,cent
secdata,162e-3,162e-3,500e-3,20e-3,20e-3,16e-3
sectype,29,beam,I,,1
secoffset,cent
secdata,166e-3,166e-3,560e-3,21e-3,21e-3,12.5e-3
sectype,30,beam,I,,1
secoffset,cent
secdata,168e-3,168e-3,560e-3,21e-3,21e-3,14.5e-3
sectype,31,beam,I,,1
secoffset,cent
secdata,170e-3,170e-3,560e-3,21e-3,21e-3,16.5e-3
sectype,32,beam,I,,1
secoffset,cent
secdata,176e-3,176e-3,630e-3,22e-3,22e-3,13e-3
sectype,33,beam,I,,1
secoffset,cent
secdata,178e-3,178e-3,630e-3,22e-3,22e-3,15e-3
sectype,34,beam,I,,1
secoffset,cent
secdata,180e-3,180e-3,630e-3,22e-3,22e-3,17e-3


!!定义节点
num_nodes=n*(1+m)*m/2+1    		 			!!节点总数
N,0,0,0,H            							!!圆心节点
N,num_nodes+1,0,0,-100            					!!坐标系原点
*do,i,1,m
  *do,j,1,n*i
    inode=(i-1)*i*n/2+1+j        	!!当前节点号
    iradius=d_ring*i          		!!当前环半径
    itheta=2*PI*(j-1)/(n*i)       !!当前节点角度
    iheight=sqrt(arc**2-iradius**2)-arc+H 				!!当前节点矢高
    N,inode,iradius*cos(itheta),iradius*sin(itheta),iheight
  *enddo
*enddo

!!定义单元
*do,i,1,m-1                         !!定义环向杆件
  type,1
  secnum,INPUTDATA(i+2)
  mat,1
  *do,j,1,n*i-1
    inode=(i-1)*i*n/2+1+j           !!节点号
    e,inode,inode+1,num_nodes+1
  *enddo
  e,(i-1)*i*n/2+n*i+1,(i-1)*i*n/2+2,num_nodes+1
*enddo

type,1
secnum,INPUTDATA(7)
mat,1
*do,i,2,n+1		                       !!定义径向杆件
   e,1,i,num_nodes+1
  *do,j,1,m-1
    inode_start=(j-1)*j*n/2+j*(i-2)+2            						!!首节点号
    inode_end=(j+1)*j*n/2+(j+1)*(i-2)+2            					!!尾节点号
    e,inode_start,inode_end,Nnode+1
  *enddo
*enddo

*do,i,1,m-1		                       !!定义支撑杆件
  type,1
  secnum,INPUTDATA(i+7)
  mat,1
  *do,j,1,n
  	*dim,line1,array,1,i+1,1
  	*do,k,1,i+1
  		*if,j,eq,n,and,k,eq,i+1,then
      	line1(1,k)=(i-1)*i*n/2+2
    	*else
      	line1(1,k)=(i-1)*i*n/2+(j-1)*i+k+1
      *endif
  	*enddo
  	
  	*dim,line2,array,1,i,1
  	*do,k,1,i
  		line2(1,k)=(i+1)*i*n/2+(j-1)*(i+1)+k+2
  	*enddo
  	
  	*dim,line,array,1,2*i+1,1
  	*do,k,1,i
  		line(1,2*k-1)=line1(1,k)
  		line(1,2*k)=line2(1,k)
  	*enddo
  	line(1,2*i+1)=line1(1,i+1)
  	
  	*do,b,1,2*i
  		e,line(1,b),line(1,b+1),Nnode+1
  	*enddo
  	
  	line1=
  	line2=
  	line=

  *enddo
*enddo



!!!!施加约束
nsel,s,,,(m-1)*m*n/2+2,(m+1)*m*n/2+1
d,all,ux,0
d,all,uy,0
d,all,uz,0
!d,all,rotx,0
!d,all,roty,0
!d,all,rotz,0


!!!施加荷载
fd=PI*R*R*dead_load/(n*(m-1)*m/2+1)
fl=PI*R*R*live_load/(n*(m-1)*m/2+1)
fw=(ASIN(R/arc)*arc*arc-R*arc+R*R)*wind_load*2/(n*(m-1)*m/2+1)
ACEL,,,9.8
nsel,s,,,1,(m-1)*m*n/2+1
f,all,fz,-fd
nsel,s,loc,x,0,R
f,all,fx,-fw
f,all,fz,-fl
nsel,all

!!!首先进行静力分析
/solu 
antype,0
pstres,on         					!!打开预应力开关
solve 
finish                		 			!!结束当前求解


!!!然后进行屈曲分析
/solu 
antype,1      
bucopt,lanb,1 
mxpand,1,,,1 
solve 
finish


/post1
NSORT,U,SUM,0								
*get,u_max,SORT,0,MAX

*GET,STRESS_MAX,SECR,,S,EQV,MAX
*GET,STRESS_MIN,SECR,,S,EQV,MIN



/prep7
UPGEOM,span/300/abs(u_max),LAST,LAST,'fem1','rst',''
finish


/solu
antype,0
nlgeom,on
arclen,on
arctrm,U,1,,uz													!对弧长法求解进行终止控制
neqit,30														!容许的最大平衡迭代次数
nsubst,30														!设置子步数
outres,all,all
solve
finish


/post26
prvar,4
nsol,2,1,u,z
prod,3,2,,,,,,1000
prod,4,1,,,,,,30
/axlab,x,Uz(mm)
/axlab,y,K
xvar,3
plvar,4
!!获得荷载稳定系数
*GET,NUM_IT,VARI,4,NSETS
*do,i,1,NUM_IT-1
	*GET,k1,VARI,4,RSET,i
	*GET,k2,VARI,4,RSET,i+1
	*IF,K1,gt,K2,then
		STAB_EFF=k1
		*exit
	*else
		STAB_EFF=k2
	*endif
*enddo

finish

!*cfopen,D:\SOFTWARE\ANSYS\dome_2\output,txt,,
*cfopen,output,txt,,
*vwrite,u_max,STRESS_MAX,STAB_EFF
(e15.6,2x,e15.6,2x,f10.5)
*cfclos
finish

